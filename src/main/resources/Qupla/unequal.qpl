// LUT logic: return (Bool) (trit1 != trit2)
lut unequal {
  -,- = -
  -,0 = 1
  -,1 = 1
  0,- = 1
  0,0 = -
  0,1 = 1
  1,- = 1
  1,0 = 1
  1,1 = -
}

// returns (Bool) (lhs != rhs)

func Bool unequal<Trit>(Trit lhs, Trit rhs) {
  return unequal[lhs, rhs]
}

template unequal<T> {
  type P [T / 3]

  func Bool unequal<T>(T lhs, T rhs) {
    return or3[unequal<P>(lhs[P * 0 : P], rhs[P * 0 : P]),
               unequal<P>(lhs[P * 1 : P], rhs[P * 1 : P]),
               unequal<P>(lhs[P * 2 : P], rhs[P * 2 : P])]
  }
}

use unequal<Tryte>, <Tiny>, <Int>, <Huge>, <Hash>

template unequal2<T> {
  type P [T / 2]

  func Bool unequal<T>(T lhs, T rhs) {
    return or[unequal<P>(lhs[P * 0 : P], rhs[P * 0 : P]),
              unequal<P>(lhs[P * 1 : P], rhs[P * 1 : P])]
  }
}

use unequal2<Tryte2>, <Tiny2>, <Tiny4>, <Int2>, <Huge2>


test - = unequal<Int>(-1, -1)
test 1 = unequal<Int>(-1, 0)
test 1 = unequal<Int>(-1, 1)
test 1 = unequal<Int>(0, -1)
test - = unequal<Int>(0, 0)
test 1 = unequal<Int>(0, 1)
test 1 = unequal<Int>(1, -1)
test 1 = unequal<Int>(1, 0)
test - = unequal<Int>(1, 1)
test 1 = unequal<Int>(10, 20)
test 1 = unequal<Int>(-30, 40)
test 1 = unequal<Int>(50, -60)
test 1 = unequal<Int>(-70, -80)
test - = unequal<Int>(90, 90)
