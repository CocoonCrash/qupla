// LUT logic: return -trit1
lut neg {
  - = 1
  0 = 0
  1 = -
}

// note that making an entire trit-vector's value negative
// can be done by simply negating every trit in the vector

// returns -val

func Trit neg<Trit> (Trit val) {
  return neg[val]
}

template neg<T> {
  type P [T / 3]

  func T neg<T> (T val) {
    return neg<P>(val[P * 0 : P]) &
           neg<P>(val[P * 1 : P]) &
           neg<P>(val[P * 2 : P])
  }
}

use neg<Tryte>, <Tiny>, <Int>, <Huge>, <Hash>

template neg2<T> {
  type P [T / 2]

  func T neg<T> (T val) {
    return neg<P>(val[P * 0 : P]) &
           neg<P>(val[P * 1 : P])
  }
}

use neg2<Tryte2>, <Tiny2>, <Tiny4>, <Int2>, <Huge2>


test 99 = neg<Tiny>(-99)
test 1 = neg<Tiny>(-1)
test 0 = neg<Tiny>(0)
test -1 = neg<Tiny>(1)
test -99 = neg<Tiny>(99)

test 99 = neg<Tiny2>(-99)
test 1 = neg<Tiny2>(-1)
test 0 = neg<Tiny2>(0)
test -1 = neg<Tiny2>(1)
test -99 = neg<Tiny2>(99)
